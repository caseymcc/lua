@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/@targets_export_name@.cmake")
check_required_components("@PROJECT_NAME@")

function(__lua_set_executable_path)
  if(EXISTS "${LUA_EXECUTABLE}")
    return()
  endif()

  if(NOT TARGET Lua::lua)
    message(FATAL_ERROR "Internal error")
  endif()

  get_target_property(configurations Lua::lua IMPORTED_CONFIGURATIONS)
  if(NOT configurations)
    message(FATAL_ERROR "Internal error")
  endif()
  list(LENGTH configurations len)
  if(len EQUAL "0")
    message(FATAL_ERROR "Internal error")
  endif()

  list(FIND configurations "RELEASE" release_index)
  if(release_index EQUAL "-1")
    list(GET configurations 0 use_config)
  else()
    set(use_config RELEASE)
  endif()

  get_target_property(LUA_EXECUTABLE Lua::lua IMPORTED_LOCATION_${use_config})

  if(NOT EXISTS "${LUA_EXECUTABLE}")
    message(FATAL_ERROR "Internal error")
  endif()

  set(LUA_EXECUTABLE "${LUA_EXECUTABLE}" PARENT_SCOPE)
endfunction()

__lua_set_executable_path()
